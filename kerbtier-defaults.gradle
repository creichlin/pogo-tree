//Last update: Sun Jan 10 12:03:35 CET 2016
// kerbtier.ch defaults, do not change, will be overwritten

/*
 * Gets the version name from the latest Git tag of the form V-X.Y where X and Y are version numbers
 */
def getVersionName = {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--match', 'V-*.*'
    standardOutput = stdout
  }
  def v = stdout.toString().trim() =~ /V-(\d+)\.(\d+)(?:-(\d+)-)?/
  return v[0][1] + "." + v[0][2] + "." + (v[0][3] == null ? 0 : v[0][3])
}

version = getVersionName()

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': version
  }
}


task updateKerbtierDefaults << {
  def url = 'https://gist.githubusercontent.com/creichlin/125a4387c15038b1672a/raw/kerbtier-default.gradle'
  def f = new File('kerbtier-defaults.gradle')
  new URL(url).withInputStream { i ->
    f.withOutputStream {
      it << "//Last update: " + new Date() + "\n"
      it << i
    }
  }
}

group = 'ch.kerbtier.' + name

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.google.guava:guava:19.0'
  compile 'commons-io:commons-io:2.4'
  
  testCompile 'org.codehaus.groovy:groovy-all:2.4.5'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

